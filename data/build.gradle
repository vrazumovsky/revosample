apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28



    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        ext.configure = { buildType, properties ->

            def buildPropertiesFile = file(properties)
            if (buildPropertiesFile.exists()) {
                Properties buildProperties = new Properties()
                buildProperties.load(new FileInputStream(buildPropertiesFile))

                buildType.buildConfigField "String", "BACKEND_URL", "\"${buildProperties['backend_url']}\""
            } else {
                throw new GradleException("properties file $properties doesn't exist")
            }
        }

        debug {
            minifyEnabled false
            configure(buildTypes.debug, "url.properties")
        }


        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            configure(buildTypes.release, "url.properties")
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Network libraries
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation 'javax.inject:javax.inject:1@jar'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.23.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
